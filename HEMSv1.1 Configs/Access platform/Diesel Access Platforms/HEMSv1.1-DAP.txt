//133ECED								:	First Build
//133ECEE								:	Added condition to prevent towing while ignition is filtered
//										:	Resolved an issue where If tagged in and the device boots up, tagout timer and pin timer is started for 5s
//133ECEF								:	Seperated RPM based Ignition logic from Analog Vehicles
//										:	Added Ignition filter when Journey periodic is sent to prevent false periodic message	
//										:	Resolved a minor issue where there might not be a tagout if he tag in while keeping the ACC On without starting the vehicle
//133ECF3 								:	Added one second timer for hourmeter
//133ECF5								:	Resolved an issue where Multiple tagin before tagout results in a fake tag in and added tagout timer with every tap of the card
//										:	Resolved a minor issue where boot reason in power up message was mapped wrong in the message format
//133ECF6 								:	Added tagout in case if the asset is in different mode
//133ED01								:	Changed Powerloss logic
//										:	Resolved an issue where Fault codes would not trigger if there are multiple faults and no faults reset
//										:	Asset shutdown logic improved
//										:	Removed Power disconnect event
//										:	Removed Geofences
//133ED04								:	Added one second timer protection
//133ED52								:	Added Erase backup
//										:	Added timer6 for 15s to tagin so that output is not always grounded
//										:	Removed IO2 dependency on Tagout logic
//										:	Removed tagout incase of analog installation to prevent multiple tagout
//										:	Changed tagout timer to 1 second once the device boots
//										:	Removed timer6 from boot as we will not allow driver to operate in case of a reboot
//133ED5B 								:	Resolved a scenario where the message pattern may break due to 8 second delay for Ignition detection
//										:	Resolved an issue where you would not get tagout if the tagout timer expires due to an operational issue
//										:	Message format change for tagout
//										:	Resolved an issue where tagging in would not flip the relay to un immobilize an asset
//										:	Changed reboot command to be compatible with BOLERO40 Series
//										:	Added reset condition remoteupdate
//133ED65 								:	Added antenna configs
//										:	Fixed an issue where you would get tagout without a tagin as it is working on a different tag mode
//										:	Added startup reason along with power up message format
//										:	Re worked towing to handle sudden speed downs to zero
//										:	Powerup handler re workded releasing more Alarms
//										:	Overspeed reworked to stop sending overspeed end when vehicle move down bridges
//										:	Added date and time to Fault events
//										:	Improved powerloss filtering

$PFAL,CNF.Set,DEVICE.NAME=HEMSv1.1-DAP-RW_133ED65

// ALIAS I/O mapping :
$PFAL,CNF.Set,ALIAS.IO11=_DIAGLED
$PFAL,CNF.Set,ALIAS.IO12=_GSMLED
$PFAL,CNF.Set,ALIAS.IO13=_PWRLED

//Orange LED (Asset config mode check)
//Stable (Asset Mode not configured)
$PFAL,CNF.Set,AL62=Sys.nvCounter.s0=0&IO.s11=Low:IO_DIAGLED.Set=High
$PFAL,CNF.Set,AL65=Sys.nvCounter.S0>0&IO.s11=High:IO_DIAGLED.Set=Low

//MACROS
//Power up timer
$PFAL,CNF.Set,MACRO0=Sys.Timer2.Start=Single,180000
//Periodic timer (Journey/CAN)
$PFAL,CNF.Set,MACRO1=Sys.Timer3.Start=Cyclic,300000
//Sleep timer
$PFAL,CNF.Set,MACRO2=Sys.Timer5.Start=Single,14400000
//Idling timer
$PFAL,CNF.Set,MACRO3=Sys.Timer8.Start=Single,900000
//Towing periodic
$PFAL,CNF.Set,MACRO4=Sys.Timer9.Start=Cyclic,300000
//5 Minutes timer for CAN request
$PFAL,CNF.Set,MACRO5=Sys.Timer13.Start=Cyclic,300000

// ROAM settings:
$PFAL,CNF.Set,GSM.OPERATOR.SELECTION=auto
$PFAL,CNF.Set,TCP.CLIENT.CONNECT=1,qaroam3.roamworks.com,6102
$PFAL,CNF.Set,GPRS.APN=m2m

//Replaces (Message formats)
// MESSAGE FORMAT - no CAN, no Odometer (for PWRL,PWRR,TOW,POLLR,HEA):
$PFAL,CNF.Set,REPLACE0=0 &(IMEI),&(Time),&(Date),&(Lat),&(Lon),&(Fix),&(Course),&(Speed),,&(Power),&(Vaccu),,,,&(nvCounter0),&(DOP),&(SatsUsed)

// Message format for Analog/Canbus Assets
$PFAL,Cnf.Set,REPLACE1=31 &(IMEI),&(Time),&(Date),&(Lat),&(Lon),&(Fix),&(Course),&(Speed),&(NavDist),&(Alt),&(Power),&(Bat),&(nvCounter0),&(Out0),&(DOP),&(SatsUsed)
$PFAL,Cnf.Set,REPLACE2=,&(CanMsg0),&(CanMsg1),&(CanMsg2),&(CAN0),&(CAN1),&(CAN2),&(CanMsg6),&(CAN3),&(CAN4),&(CAN5),&(CAN6)
$PFAL,Cnf.Set,REPLACE3=,&(nvCounter16):&(nvCounter15),&(nvCounter14),&(CanMsg8)

//Message format for HEA,
$PFAL,Cnf.Set,REPLACE4=0 &(IMEI),&(Time),&(Date),&(Lat),&(Lon),&(Fix),&(Course),&(Speed),,&(Power),&(Bat),,,,&(nvCounter0),&(DOP),&(SatsUsed)

//Message format for TAGI
$PFAL,Cnf.Set,REPLACE5=0 &(IMEI),&(Time),&(Date),&(Lat),&(Lon),&(Fix),&(Course),&(Speed),,&(Power),&(Bat),,,,&(nvCounter0),&(DOP),&(SatsUsed),&(SerialData0.End8)

// Message format for Powerup
$PFAL,Cnf.Set,REPLACE6=0 &(IMEI),&(Time),&(Date),&(Lat),&(Lon),&(Fix),&(Course),&(Speed),,&(Power),&(Vaccu),,,,&(nvCounter0),&(DOP),&(SatsUsed),,&(StartUpreason),&(DeviceName)

//Message format for OSPS
$PFAL,CNF.Set,REPLACE7=35 &(IMEI),&(Time),&(Date),&(Lat),&(Lon),&(Fix),&(Course),&(nvCounter11),&(NavDist),&(Alt),&(Power),&(Bat),&(nvCounter0),&(Out0),&(DOP),&(SatsUsed),

//Message format for Poll response
$PFAL,CNF.Set,REPLACE8=,&(nvCounter1),&(nvCounter10),&(nvCounter4),&(nvCounter6),&(nvCounter7),&(nvCounter8),&(nvCounter2),&(nvCounter3),&(nvCounter11),&(nvCounter12),&(nvCounter13)
$PFAL,CNF.Set,REPLACE9=Sys.MACRO0&Sys.MACRO2&Sys.Timer16.Start=Cyclic,2000&Sys.Timer1.start=cyclic,1000

//CANbus Handler
//CANbus Enable
$PFAL,Sys.Can.Enable,250K,RW
$PFAL,Sys.CAN.FMS.disable

//CanMsg0 = Engine temperature 1
$PFAL,Sys.Can.Msg.Add,ext,00FEEE00,00FFFF00
//Engine Coolant temperature
//$PFAL,Sys.CAN.Var.Add,0,Number,State,ext,00FEEE00,0,0,0,7,MSB,0,1,1,-40
//Engine Fuel temperature
//$PFAL,Sys.CAN.Var.Add,1,Number,State,ext,00FEEE00,1,0,1,7,MSB,0,1,1,-40
//Engine Oil temperature
//$PFAL,Sys.CAN.Var.Add,2,Number,State,ext,00FEEE00,2,0,3,7,LSB,0,1,32,-273

//CanMsg1 = ECU 1 ---------------F004
$PFAL,Sys.Can.Msg.Add,ext,00F00400,00FFFF00
//Engine RPM
$PFAL,Sys.CAN.Var.Add,3,Number,State,ext,00F00400,3,0,4,7,LSB,0,1,8,0
//Driver demand engine percentage torque
//$PFAL,Sys.CAN.Var.Add,4,Number,State,ext,00F00400,1,0,1,7,MSB,-125,1,1,0
//Actual engine percentage torque
//$PFAL,Sys.CAN.Var.Add,5,Number,State,ext,00F00400,2,0,2,7,MSB,-125,1,1,0

//CanMsg2 = Engine Fluid Level/Pressure 1
$PFAL,Sys.Can.Msg.Add,ext,00FEEF00,00FFFF00
//Engine Oil Level
//$PFAL,Sys.CAN.Var.Add,6,Number,State,ext,00FEEF00,2,0,2,7,MSB,0,2,5,0
//Engine Oil Pressure
//$PFAL,Sys.CAN.Var.Add,7,Number,State,ext,00FEEF00,3,0,3,7,MSB,0,4,1,0
//Engine coolant pressure
//$PFAL,Sys.CAN.Var.Add,8,Number,State,ext,00FEEF00,6,0,6,7,MSB,0,2,1,0
//Engine Coolant Level
//$PFAL,Sys.CAN.Var.Add,9,Number,State,ext,00FEEF00,7,0,7,7,MSB,0,2,5,0

//CanMsg3 = Engine Hour Meter
$PFAL,Sys.Can.Msg.Add,ext,00FEE500,00FFFF00
$PFAL,Sys.CAN.Var.Add,0,Number,State,ext,00FEE500,0,0,3,7,LSB,0,1,20,0

//CanMsg4 = Fuel Rate
$PFAL,Sys.Can.Msg.Add,ext,00FEF200,00FFFF00
$PFAL,Sys.CAN.Var.Add,1,Number,State,ext,00FEF200,0,0,1,7,LSB,0,1,20,0

//CanMsg5 = Total Fuel Consumed
$PFAL,Sys.Can.Msg.Add,ext,00FEE900,00FFFF00
$PFAL,Sys.CAN.Var.Add,2,Number,State,ext,00FEE900,4,0,7,7,LSB,0,1,2,0

//CanMsg6 = Diagnostic Trouble Codes
//CanMsg6 = DM1 ---------------PGN=65226/FECA
$PFAL,Sys.Can.Msg.Add,ext,00FECA00,00FFFF00
//Bits 5-7 of byte 4 is also part of the SPN
//$PFAL,Sys.CAN.Var.Add,13,Number,event,ext,00FECA00,2,0,4,7,LSB,0,1,1,0
$PFAL,Sys.CAN.Var.Add,4,Number,event,ext,00FECA00,2,0,3,7,LSB,0,1,1,0
$PFAL,Sys.CAN.Var.Add,5,Number,event,ext,00FECA00,4,0,4,4,LSB,0,1,1,0


//CanMsg7 = ECU 2
$PFAL,Sys.Can.Msg.Add,ext,00F00300,00FFFF00
//Engine percentage load at current speed
$PFAL,Sys.CAN.Var.Add,6,Number,State,ext,00F00300,2,0,2,7,LSB,0,1,1,0

//CanMsg8 = Dash Display
$PFAL,Sys.Can.Msg.Add,ext,00FEFC00,00FFFF00
//$PFAL,Sys.CAN.Var.Add,11,Number,State,ext,00FEFC00,1,0,1,7,LSB,0,1,25,0

////Erase backup
$PFAL,CNF.EraseBackup

////Power up Handler (//Add Boot_counter,Device name,location info)
$PFAL,CNF.Set,AL0=Sys.Device.eStart:Sys.Trigger9.Load0
$PFAL,CNF.Set,AL1=Sys.Device.eStart=PowerUp?Sys.Device.eStart=Reset?Sys.Device.eStart=Wakeup?Sys.Device.eStart=Doze:Sys.MACRO0&Sys.MACRO2&Sys.Timer16.Start=Cyclic,2000&Sys.Timer1.Start=Cyclic,1000
//To retain the status in case unit reboot in between journey
$PFAL,CNF.Set,AL2=Sys.Device.eStart&Sys.nvCounter.s0=2:Sys.MACRO1&Sys.Timer5.Stop&Sys.Timer13.stop&GPS.Nav.Distance.Load&Sys.Timer1.Start=Cyclic,1000
$PFAL,CNF.Set,AL3=Sys.Device.eStart&Sys.nvCounter.s0=1&Sys.nvCounter.s3>=2:Sys.Timer7.Start=Single,3000
$PFAL,CNF.Set,AL4=Sys.Timer.e2&Sys.Trigger.s9=High&Sys.nvCounter.s0=1:Sys.Timer7.Start=Single,1000
$PFAL,CNF.Set,AL5
$PFAL,CNF.Set,AL6
$PFAL,CNF.Set,AL7=GPS.eExtAntPlugged:GSM.SetExternalAntenna
$PFAL,CNF.Set,AL8=GPS.eExtAntUnplugged:GSM.SetInternalAntenna

//Power up message
$PFAL,CNF.Set,AL9=Sys.Timer.e2:TCP.Client.Send,0,"PWRUP (REPLACE6)"&Sys.nvCounter2.Set=1

//To retain the status in case unit reboot in between journey
$PFAL,CNF.Set,AL10
$PFAL,CNF.Set,AL11
////Power status Handler

$PFAL,CNF.Set,AL13=Sys.Timer.e1&Sys.Power.sVoltage<9&Sys.nvCounter.s8<7:Sys.nvCounter8.Increment=1
$PFAL,CNF.Set,AL16=Sys.nvCounter.s8>0&Sys.nvCounter.s8<7&Sys.Power.sVoltage>10:Sys.nvCounter8.Set=0
$PFAL,CNF.Set,AL14=Sys.Power.sVoltage<9&Sys.nvCounter.s8=7:Sys.nvCounter8.Set=8&TCP.Client.Send,0,"PWRL (REPLACE0)"
$PFAL,CNF.Set,AL15=Sys.Power.sVoltage>10&Sys.nvCounter.s8=8:Sys.nvCounter8.Set=0&TCP.Client.Send,0,"PWRR (REPLACE0)"

////Ignition on/off Handler
//Ignition Detection Logic (Driver has to turn the key all the way off for Ignition Off-Refer Document)
$PFAL,CNF.Set,AL17=IO.s8=High&Sys.CAN.s3>=200&Sys.Trigger.s1=Low:Sys.Trigger1=High
$PFAL,CNF.Set,AL18=IO.s8=Low&Sys.Trigger.s1=High:Sys.Trigger1=Low
//For analog Assets without RPM
$PFAL,CNF.Set,AL19=Sys.nvCounter.s12=1&IO.s8=High&IO.s2=High&Sys.Trigger.s1=Low:Sys.Trigger1=High
$PFAL,CNF.Set,AL20=Sys.nvCounter.s12=1&IO.s8=Low&IO.s2=Low&Sys.Trigger.s1=High:Sys.Trigger1=Low

//Ignition ON/OFF Events
$PFAL,CNF.Set,AL25=Sys.Timer.e1&Sys.Trigger.s1=High&Sys.CAN.s3>=200&Sys.nvCounter.s5<8:Sys.nvCounter5.Increment=1&Sys.Counter4.Set=0
$PFAL,CNF.Set,AL26=Sys.Timer.e1&Sys.Trigger.s1=Low&IO.s8=Low&Sys.nvCounter.s5>0:Sys.nvCounter5.Decrement=1
$PFAL,CNF.Set,AL28=Sys.nvCounter.s5=8&Sys.nvCounter.s0=1:Sys.nvCounter0.set=2&Sys.MACRO1&Sys.Timer5.Stop&Sys.MACRO5&TCP.Client.Send,0,"IN8H (REPLACE1)(REPLACE2)(REPLACE3)"
$PFAL,CNF.Set,AL27=Sys.nvCounter.s5=8&Sys.nvCounter.s0=1:GPS.Nav.Distance.Load&Sys.Timer7.Stop&GPS.Nav.Position1=current
$PFAL,CNF.Set,AL30=Sys.nvCounter.s5<=1&Sys.nvCounter.s0=2:Sys.nvCounter0.set=1&Sys.Timer3.Stop&Sys.MACRO2&Sys.Timer13.stop&TCP.Client.Send,0,"IN8L (REPLACE1)(REPLACE2)(REPLACE3)"
$PFAL,CNF.Set,AL29=Sys.nvCounter.s5<=1&Sys.nvCounter.s0=2:GPS.Nav.Distance.Save&Sys.Timer7.Start=Single,25000

//For analog Assets without RPM
$PFAL,CNF.Set,AL21=Sys.Timer.e1&Sys.nvCounter.s12=1&Sys.Trigger.s1=High&IO.s2=High&Sys.nvCounter.s5<8:Sys.nvCounter5.Increment=1&Sys.Counter4.Set=0
$PFAL,CNF.Set,AL31=Sys.Timer.e1&Sys.nvCounter.s12=1&Sys.Trigger.s1=Low&IO.s2=Low&Sys.nvCounter.s5>0:Sys.nvCounter5.Decrement=1

////Periodic Handler
//AL32 for Analog Assets
//$PFAL,CNF.Set,AL32=Sys.nvCounter.s0=2&Sys.Timer.e3:TCP.Client.Send,0,"PER (REPLACE1)(REPLACE2)(REPLACE3)"
$PFAL,CNF.Set,AL33=Sys.nvCounter.s0=2&Sys.Timer.e3:TCP.Client.Send,0,"CANP (REPLACE1)(REPLACE2)(REPLACE3)"

////Geofence Handler

$PFAL,CNF.Set,AL48=GPS.Geofence.eX=inside&GPS.Nav.sFix=valid&Sys.nvCounter.s1=1:Sys.nvCounter1.Set=2&TCP.Client.Send,0,"GEOIN (REPLACE1)(REPLACE2)(REPLACE3)"
$PFAL,CNF.Set,AL49=GPS.Geofence.eX=outside&GPS.Nav.sFix=valid&Sys.nvCounter.s1=2:Sys.nvCounter1.Set=1&TCP.Client.Send,0,"GEOEX (REPLACE1)(REPLACE2)(REPLACE3)"

////Poll Handler
//Poll response over GPRS
$PFAL,CNF.Set,AL50=Sys.Counter.s2=1:Sys.Counter2.clear&TCP.Client.Send,0,"POLLR (REPLACE6)(REPLACE8)"
$PFAL,CNF.Set,AL22=Sys.Counter.s2=2:Sys.Counter2.clear&TCP.Client.Send,0,"CANPOLLR (REPLACE1)(REPLACE2)(REPLACE3)"

//Poll response over SMS
$PFAL,CNF.Set,AL51=GSM.SMS.eIncoming.Text="POLL":GSM.SMS.Send,"&(SMSNumber)",0,"Poll response for &(IMEI)Location:&(Lat),&(Lon)Speed:&(Speed)Dist:&(NavDist)GPRS:&(GPRS)TCP:&(TCPClient)Operator:&(OperatorID)"
$PFAL,CNF.Set,AL52=GSM.SMS.eIncoming.Text="CANPOLL":GSM.SMS.Send,"&(SMSNumber)",0,"Poll response for &(IMEI)Location:&(Lat),&(Lon)Speed:&(Speed)Dist:&(NavDist)GPRS:&(GPRS)TCP:&(TCPClient)Operator:&(OperatorID)DTC:&(CanMsg6)RPM:&(CAN3)"

//CAN poll request
$PFAL,CNF.Set,AL96=SYS.TIMER.e13:Sys.Can.Request,ext,FEE5&Sys.Can.Request,ext,FEFC&Sys.Can.Request,ext,FEE9&Sys.Can.Request,ext,FEF2

////LED Handler
//Orange LED
//Off
$PFAL,CNF.Set,AL53=Sys.Timer.e16&Sys.Power.sVoltage<9&GPS.Nav.sFix=invalid&Sys.nvCounter.s0=1:IO_PWRLED.Set=Low
//Single Short Blink
$PFAL,CNF.Set,AL54=Sys.Timer.e16&Sys.Power.sVoltage>10&GPS.Nav.sFix=invalid&Sys.nvCounter.s0=1:IO_PWRLED.Set=cyclic,125,125,1
//Double Short Blink
$PFAL,CNF.Set,AL55=Sys.Timer.e16&Sys.Power.sVoltage>10&GPS.Nav.sFix=valid&Sys.nvCounter.s0=1:IO_PWRLED.Set=cyclic,125,125,2
//Triple Short Blink
$PFAL,CNF.Set,AL56=Sys.Timer.e16&Sys.Power.sVoltage<9&GPS.Nav.sFix=valid&Sys.nvCounter.s0=2:IO_PWRLED.Set=cyclic,125,125,3
//Four Short Blink
$PFAL,CNF.Set,AL57=Sys.Timer.e16&Sys.Power.sVoltage>10&GPS.Nav.sFix=invalid&Sys.nvCounter.s0=2:IO_PWRLED.Set=cyclic,125,125,4
//Stable
$PFAL,CNF.Set,AL58=Sys.Timer.e16&Sys.Power.sVoltage>10&GPS.Nav.sFix=valid&Sys.nvCounter.s0=2:IO_PWRLED.Set=High

//Green LED
//Off
$PFAL,CNF.Set,AL59=Sys.Timer.e16&GSM.GPRS.sOffline&TCP.Client.sDisconnected:IO_GSMLED.Set=Low
//Single Short Blinks
$PFAL,CNF.Set,AL60=Sys.Timer.e16&GSM.GPRS.sOnline&TCP.Client.sDisconnected:IO_GSMLED.Set=cyclic,125,125,1
//Stable
$PFAL,CNF.Set,AL61=Sys.Timer.e16&GSM.GPRS.sOnline&TCP.Client.sConnected:IO_GSMLED.Set=High

////Heartbeat Handler
$PFAL,CNF.Set,AL63=GPS.Time.eHour=8?GPS.Time.eHour=20:Sys.Trigger4=High
$PFAL,CNF.Set,AL64=Sys.Trigger.s4=High?Sys.Timer.e15:TCP.Client.Send,0,"HEA (REPLACE6)"&Sys.Trigger4=Low
////Weekly Reboot Handler
$PFAL,CNF.Set,AL66=GPS.Time.sWDay=5&GPS.Time.sHour=19&GPS.Time.sMinute=50&GPS.Time.eSecond=55:Sys.Trigger5=High
$PFAL,CNF.Set,AL67=Sys.Trigger.s5=high&Sys.nvCounter.s0=1:Sys.Device.Reset,1
$PFAL,CNF.Set,AL68=Sys.Device.eStart=Wakeup,Ign?Sys.Device.eStart=Wakeup,Timer?Sys.Device.eStart=Wakeup,Ring:Sys.Timer15.Start=Single,180000
////Sleep Mode Handler
$PFAL,CNF.Set,AL69=Sys.Timer.e5&Sys.nvCounter.s10=1:Sys.Device.Sleep=Ign+Ring+ExtPwrDrop+Timer=12:00:00&TCP.Client.Send,0,"Sleep Mode Started &(IMEI),&(Time),&(Date),&(Lat),&(Lon),&(Fix)"
$PFAL,CNF.Set,AL70=Sys.Device.eStart=Wakeup,Timer:Sys.Timer5.Start=Single,300000&TCP.Client.Send,0,"Sleep Mode Ended &(IMEI),&(Time),&(Date),&(Lat),&(Lon),&(Fix)"
//Invalid Sleep safety
$PFAL,CNF.Set,AL71=Sys.nvCounter.s0=2&Sys.Timer.s5=running:Sys.Timer5.Stop

////Towing Handler
$PFAL,CNF.Set,AL73=Sys.nvCounter.s0=1&GPS.Nav.sSpeed>6&GPS.Nav.sSpeed<56&GPS.Nav.sFix=valid&Sys.Counter.s4<7:Sys.Counter4.Increment=1
$PFAL,CNF.Set,AL74=Sys.Counter.s4=7&Sys.nvCounter.s0=1&Sys.nvCounter.s6=1:Sys.nvCounter6.Set=2&Sys.MACRO4&Sys.Timer5.Stop&TCP.Client.Send,0,"TOW (REPLACE1)(REPLACE2)(REPLACE3)"
$PFAL,CNF.Set,AL75=Sys.Timer.e1&Sys.nvCounter.s6=2&GPS.Nav.sSpeed<1&Sys.Counter.s4>0:Sys.Counter4.Decrement=1
$PFAL,CNF.Set,AL87=Sys.Counter.s4<1&GPS.Nav.sSpeed<1&Sys.nvCounter.s6=2:Sys.nvCounter6.Set=1&Sys.Timer9.Stop&Sys.MACRO2
$PFAL,CNF.Set,AL76=Sys.nvCounter.s6=2&Sys.nvCounter.s0=2:Sys.Counter4.Set=0&Sys.nvCounter6.Set=1&Sys.Timer9.Stop
$PFAL,CNF.Set,AL77=Sys.Timer.e9&Sys.nvCounter.s6=2:TCP.Client.Send,0,"TOWP (REPLACE1)(REPLACE2)(REPLACE3)"

////Position Report

//$PFAL,CNF.Set,AL80=Sys.nvCounter.s0=2&GPS.Nav.sFix=valid&GPS.Nav.eChangeHeading:TCP.Client.Send,0,"PREP (REPLACE1)(REPLACE2)(REPLACE3)"

////TCP Disconnection (Automatic Reboot)

$PFAL,CNF.Set,AL81=TCP.Client.sDisconnected&SYS.TIMER.s4=inactive&Sys.nvCounter.s2=1:Sys.nvCounter2.Set=2&SYS.TIMER4.start=single,1800000
$PFAL,CNF.Set,AL82=TCP.Client.sConnected&Sys.nvCounter.s2=2:Sys.nvCounter2.Set=1&SYS.TIMER4.stop
$PFAL,CNF.Set,AL83=SYS.TIMER.e4&Sys.nvCounter.s2=2:Sys.nvCounter2.Set=1&Sys.device.reset,2


/////////////Below Alarm lines are Asset dependant///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

//Fault Detection
$PFAL,CNF.Set,AL90=Sys.Can.e4&Sys.CAN.s4<65535&Sys.nvCounter.s11=1:Sys.nvCounter13.Set=2&TCP.Client.Send,0,"Fault Detected &(IMEI),&(CanMsg6),&(CAN4),&(CAN5),&(Date),&(Time)"
$PFAL,CNF.Set,AL91=Sys.Can.e4&Sys.CAN.s4=65535&Sys.nvCounter.s13=2:Sys.nvCounter13.Set=1&TCP.Client.Send,0,"Fault Reset &(IMEI),&(CanMsg6),&(CAN4),&(CAN5),&(Date),&(Time)"


//Asset Shutdown Alert (FMI Based)
$PFAL,CNF.Set,AL92=Sys.CAN.e5&Sys.CAN.s5=0:Sys.Trigger0=High
$PFAL,CNF.Set,AL93=Sys.CAN.e5&Sys.CAN.s5=1:Sys.Trigger0=High
$PFAL,CNF.Set,AL94=Sys.CAN.e5&Sys.CAN.s5>1:Sys.Trigger0=Low
$PFAL,CNF.Set,AL95=Sys.Trigger.s0=High&Sys.CAN.s4>0&Sys.nvCounter.s11=1:Sys.Trigger0=Low&TCP.Client.Send,0,"Shutdown Alert &(IMEI),&(CanMsg6),&(CAN5),&(Date),&(Time)"

////Tagin/Out Handler
//Master/Authorised IDs
$PFAL,CNF.Set,AL34=Sys.eSerialData0.End="0B"?Sys.eSerialData0.End="36"?Sys.eSerialData0.End="F4"?Sys.eSerialData0.End="FD"?Sys.eSerialData0.End="F5":Sys.Counter3.set=1&Sys.Timer6.Start=Single,15000
//Ids Synced from ROAM and Other Authorised IDs
$PFAL,CNF.Set,AL35=Sys.eSerialData0.End=Whitelist:Sys.Counter3.set=1&Sys.Timer6.Start=Single,15000

//Tagin
$PFAL,CNF.Set,AL36=Sys.Counter.s3=1&Sys.nvCounter.s3=1:Sys.nvCounter3.Set=2&Sys.Timer7.Start=Single,25000&Sys.Trigger9=High&Sys.Trigger9.save0&TCP.Client.Send,0,"TAGI (REPLACE5)"
$PFAL,CNF.Set,AL37=Sys.Timer.e6?Sys.Timer.e7?Sys.nvCounter.s3=7:IO4.Set=Low&Sys.Counter3.clear
$PFAL,CNF.Set,AL38=Sys.nvCounter.s3=12?Sys.Counter.s3=1:IO4.Set=High
$PFAL,CNF.Set,AL39=IO.s8=High&Sys.Timer.s7=running:Sys.Timer7.Stop
$PFAL,CNF.Set,AL47=Sys.nvCounter.s3=2&IO.s8=Low&Sys.nvCounter.s0=1&Sys.nvCounter.s5<=1&Sys.timer.s7=inactive:Sys.Timer7.Start=Single,25000
$PFAL,CNF.Set,AL40=Sys.Timer.e7&Sys.nvCounter.s3=2:Sys.nvCounter3.Set=1&Sys.Trigger9=Low&Sys.Trigger9.save0&TCP.Client.Send,0,"TAGO (REPLACE0)"&MSG.Send.Serial0,0,"&(bin=0x01,0x21)"

//Tagout even when the device is in other tag modes
$PFAL,CNF.Set,AL23=Sys.Timer.e7&Sys.Trigger.s9=High&Sys.nvCounter.s3>6:MSG.Send.Serial0,0,"&(bin=0x01,0x21)"&Sys.Trigger9=Low&Sys.Trigger9.save0&TCP.Client.Send,0,"TAGO (REPLACE0)"

$PFAL,CNF.Set,AL41=Sys.nvCounter.s3=6:Sys.nvCounter3.Set=7&TCP.Client.Send,0,"IMMOA (REPLACE1)(REPLACE2)(REPLACE3)"
$PFAL,CNF.Set,AL42=Sys.Counter.s3=1&Sys.nvCounter.s3=7&Sys.Trigger.s9=Low&Sys.timer.s7=inactive:Sys.Counter3.clear&Sys.Timer7.Start=Single,25000&Sys.Trigger9=High&Sys.Trigger9.save0&TCP.Client.Send,0,"TAGI (REPLACE5)"
$PFAL,CNF.Set,AL43=Sys.nvCounter.s3=8:IO4.Set=Low&Sys.nvCounter3.Set=1&TCP.Client.Send,0,"IMMOR (REPLACE1)(REPLACE2)(REPLACE3)"

$PFAL,CNF.Set,AL44=Sys.nvCounter.s3=11:Sys.nvCounter3.Set=12&TCP.Client.Send,0,"SERV (REPLACE1)(REPLACE2)(REPLACE3)"
$PFAL,CNF.Set,AL45=Sys.Counter.s3=1&Sys.nvCounter.s3=12&Sys.Trigger.s9=Low&Sys.timer.s7=inactive:Sys.Counter3.clear&Sys.Timer7.Start=Single,25000&Sys.Trigger9=High&Sys.Trigger9.save0&TCP.Client.Send,0,"TAGI (REPLACE5)"
$PFAL,CNF.Set,AL46=Sys.nvCounter.s3=13:IO4.Set=Low&Sys.nvCounter3.Set=1&TCP.Client.Send,0,"NORM (REPLACE1)(REPLACE2)(REPLACE3)"

$PFAL,CNF.Set,AL12=Sys.Trigger.s9=High&Sys.nvCounter.s3=1:Sys.nvCounter3.Set=2

//Hourmeter
$PFAL,CNF.Set,AL97=Sys.Timer.e1&Sys.nvCounter.s14=59:Sys.nvCounter15.Increment=1&Sys.nvCounter14.Set=0
$PFAL,CNF.Set,AL98=Sys.Timer.e1&Sys.nvCounter.s0=2&Sys.nvCounter.s14<59:Sys.nvCounter14.Increment=1
$PFAL,CNF.Set,AL99=Sys.Timer.e1&Sys.nvCounter.s15=59:Sys.nvCounter16.Increment=1&Sys.nvCounter15.Set=0

//Operation Hours
$PFAL,CNF.Set,AL84=Sys.Timer.e1&Sys.nvCounter.s4=59:Sys.nvCounter7.Increment=1&Sys.nvCounter4.Set=0
$PFAL,CNF.Set,AL85=Sys.Timer.e1&IO.s8=High&Sys.nvCounter.s4<59:Sys.nvCounter4.Increment=1
$PFAL,CNF.Set,AL86=Sys.Timer.e1&Sys.nvCounter.s7=59:Sys.nvCounter9.Increment=1&Sys.nvCounter7.Set=0
$PFAL,CNF.Set,AL24=IO.s8=Low&Sys.nvCounter.s4>0:TCP.Client.Send,0,"Current operational Time is &(nvCounter9) hours &(nvCounter7) minutes and &(nvCounter4) Seconds"&Sys.nvCounter9.Set=0&Sys.nvCounter7.Set=0&Sys.nvCounter4.Set=0

//One second timer protection
$PFAL,CNF.Set,AL92=Sys.Timer.s1=inactive:Sys.Timer1.Start=Cyclic,1000

//External Antenna config
$PFAL,CNF.Set,AL79=GPS.eExtAntPlugged:GSM.SetExternalAntenna
$PFAL,CNF.Set,AL80=GPS.eExtAntUnplugged:GSM.SetInternalAntenna

//Preventing presence of unwanted alarms for this specific asset type

$PFAL,CNF.Set,AL88
$PFAL,CNF.Set,AL89

// -----------------------------------------------------------------------------------
// OTHER SETTINGS:
// -----------------------------------------------------------------------------------
// -----------------------------------------------------------------------------------
// OTHER SETTINGS:
// -----------------------------------------------------------------------------------
$PFAL,CNF.Set,DBG.EN=0
$PFAL,CNF.Set,DEVICE.BAT.CHARGEMODE=eco
$PFAL,CNF.Set,DEVICE.BAT.MODE=auto
$PFAL,CNF.Set,DEVICE.CMD.PFAL.EN=F
$PFAL,CNF.Set,DEVICE.COMM.CSD=cmd,60
$PFAL,CNF.Set,DEVICE.COMM.TCP.CLIENT=cmd,60
$PFAL,CNF.Set,DEVICE.GPS.AUTOCORRECT=on,4.0,50,50,8,10
$PFAL,CNF.Set,DEVICE.GPS.CFG=5,fast,1,0,0
$PFAL,CNF.Set,DEVICE.GPS.TIMEOUT=1,7
$PFAL,CNF.Set,GPRS.AUTOSTART=1
$PFAL,CNF.Set,GPRS.DIAL=ATD*99***1#
$PFAL,CNF.Set,GPRS.QOS=0,0,0,0,0
$PFAL,CNF.Set,GPRS.QOSMIN=0,0,0,0,0
$PFAL,CNF.Set,GSM.BALANCE.DIAL=*100#
$PFAL,CNF.Set,GSM.BANDPREF=EUR
$PFAL,CNF.Set,GSM.CALLID.EN=1
$PFAL,CNF.Set,GSM.OPERATOR.GPRSCHECK=disabled
$PFAL,CNF.Set,GSM.PIN=0000
$PFAL,CNF.Set,GSM.SMS.RESPONSE=1,5
$PFAL,CNF.Set,MOTION.FILTER=4,20,390,156
$PFAL,CNF.Set,PPP.AUTH=auto
$PFAL,CNF.Set,PPP.AUTOPING=0,300000
$PFAL,CNF.Set,PPP.PASSWORD=none
$PFAL,CNF.Set,PPP.USERNAME=none
$PFAL,CNF.Set,PROT.3DP=0
$PFAL,CNF.Set,PROT.AREA=0
$PFAL,CNF.Set,PROT.BIN=0
$PFAL,CNF.Set,PROT.GGA=0
$PFAL,CNF.Set,PROT.GLL=0
$PFAL,CNF.Set,PROT.GSA=0
$PFAL,CNF.Set,PROT.GSM=0
$PFAL,CNF.Set,PROT.GSV=0
$PFAL,CNF.Set,PROT.IOP=0
$PFAL,CNF.Set,PROT.RMC=0
$PFAL,CNF.Set,PROT.START.BIN=$
$PFAL,CNF.Set,PROT.VTG=0
$PFAL,CNF.Set,TCP.CLIENT.ALTERNATIVE=0,0.0.0.0,0
$PFAL,CNF.Set,TCP.CLIENT.DNS.TIMEOUT=86400
$PFAL,CNF.Set,TCP.CLIENT.LOGIN=1
$PFAL,CNF.Set,TCP.CLIENT.SENDMODE=2
$PFAL,CNF.Set,TCP.SERVICE.CONNECT=0,0.0.0.0,0
$PFAL,CNF.Set,TCP.SMTP.CONNECT=0,<mailserver_ip>,<port>
$PFAL,CNF.Set,TCP.SMTP.FROM=<valid mailadress>
$PFAL,CNF.Set,TCP.SMTP.LOGIN="<domain>",180,"<username>","<password>"
$PFAL,CNF.Set,TCP.STORAGE=size=3072,dispatch=manual
$PFAL,CNF.Set,UDP.CLIENT.CONNECT=0,0.0.0.0,0
$PFAL,CNF.Set,UDP.CLIENT.TIMEOUT=300000
$PFAL,CNF.Set,DEVICE.SERIAL1.BAUDRATE=115200

// GPRS.TIMEOUT(8min) > TCP.CLIENT.TIMEOUT(4min)
// Ping (2min), OPlost (20min), Sleep retry (10min)
$PFAL,CNF.Set,GPRS.TIMEOUT=1,480000
$PFAL,CNF.Set,TCP.CLIENT.TIMEOUT=240000,30000
$PFAL,CNF.Set,TCP.SERVICE.TIMEOUT=240000,30000
$PFAL,CNF.Set,TCP.CLIENT.PING=1,120000
$PFAL,CNF.Set,GSM.OPLOST.RESTART=1,1200000,8

//Geofences

//Alarm hide
$PFAL,Sys.Security.HideAlarm," R0O4A3M9W0O0R2K7S0"

//RFID Config
$PFAL,CNF.Set,DEVICE.COMM.BINEVENT=,0D,e

//Reboot to enable asset features
$PFAL,Sys.Device.Reset